plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.nivelais.combiplanner"
        minSdkVersion 27
        targetSdkVersion 30

        versionCode generateVersionCode()
        versionName version
    }

    buildTypes {
        debug {
            debuggable true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        excludes += '/META-INF/DEPENDENCIES'
    }
}

dependencies {
    // Other modules
    implementation project(':domain')
    implementation project(':data')

    // Kotlin and coroutine
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // AndroidX base library
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.0'

    // Navigation
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.navigation:navigation-dynamic-features-fragment:$nav_version"

    // Jetpack compose
    implementation "androidx.compose.runtime:runtime:$compose_version"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.foundation:foundation-layout:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation "androidx.compose.foundation:foundation:$compose_version"
    implementation "androidx.compose.animation:animation:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"

    // Navigation with Compose
    implementation "androidx.navigation:navigation-compose:$nav_compose_version"

    // Koin (Dependency Injection)
    implementation "io.insert-koin:koin-core-ext:$koin_version"
    implementation "io.insert-koin:koin-android-ext:$koin_version"
    implementation "io.insert-koin:koin-androidx-compose:$koin_version"

    // Material design
    implementation 'com.google.android.material:material:1.3.0'

    // WhatIf, for smooth null check and cast
    implementation "com.github.skydoves:whatif:1.0.9"

    // Slf4j (Logging)
    implementation "org.slf4j:slf4j-android:$slf4j_version"

    // Log4j
    implementation "org.apache.logging.log4j:log4j-api:$logback_version"
    kapt "org.apache.logging.log4j:log4j-core:$logback_version"

    // Use https://github.com/chrisbanes/accompanist for image composable
    // If retrofit for some network, also use https://github.com/skydoves/sandwich

}

/**
 * Generate the app version code from the versionName
 */
def generateVersionCode() {
    return Integer.parseInt(version.replaceAll("[^0-9]", ""))
}