// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = '1.4.21'
    ext.coroutines_version = '1.4.2'
    ext.nav_version = '2.3.2'
    ext.compose_version = '1.0.0-alpha08'
    ext.nav_compose_version = '1.0.0-alpha03'
    ext.koin_version = '2.2.1'

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.0-alpha02'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.koin:koin-gradle-plugin:$koin_version"
    }
}

plugins {
    id 'com.diffplug.spotless' version '5.8.2'
}

subprojects {
    repositories {
        google()
        jcenter()
    }

    // Apply spotless plugin on all sub project (Clean code)
    apply plugin: 'com.diffplug.spotless'
    spotless {
        kotlin {
            target '**/*.kt'
            targetExclude("$buildDir/**/*.kt")
            targetExclude('bin/**/*.kt')

            ktlint('0.40.0')
            licenseHeaderFile rootProject.file('spotless/copyright.kt')
        }
    }

    // Enable advanced kotlin option and expiremental ones
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            // Treat all Kotlin warnings as errors (to have the better code possible)
            allWarningsAsErrors = true

            // Enable experimental coroutines APIs, including Flow
            freeCompilerArgs += '-Xopt-in=kotlin.RequiresOptIn'
            freeCompilerArgs += '-Xopt-in=kotlin.Experimental'
            freeCompilerArgs += '-Xallow-jvm-ir-dependencies'

            // Set JVM target to 1.8
            jvmTarget = "1.8"
        }
    }
}